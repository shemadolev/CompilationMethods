%{
    #include <iostream>
    #include "part2_helpers.h"
    using namespace std;

    extern int yylex();
    void yyerror(const char*);

    extern int yylineno;
    extern const char* yytext;

    ParserNode *parseTree; 
%}

// reserved words 
%token  INT
        FLOAT
        VOID
        WRITE
        READ
        WHILE
        DO
        IF
        RETURN
        FULL_WHILE
        BREAK

// solve dangling else (select shift over reduce)  
%precedence THEN
%precedence ELSE

// other
%token ID INTEGERNUM REALNUM STR

%right ASSIGN
%left OR
%left AND
%left RELOP
%left ADDOP
%left MULOP
%right NOT

// symbols
%precedence '(' ')' '{' '}' '.' ';' ',' ':'

%%

program : fdefs                         {parseTree=makeSymbol("PROGRAM",$1);}
;

fdefs : /* epsilon */                   {$$=makeSymbol("FDEFS", EPSILON);}
	| fdefs func_api blk            {$$=makeSymbol("FDEFS",concatList(concatList($1,$2),$3));}
        | fdefs func_api ';'            {$$=makeSymbol("FDEFS",concatList(concatList($1,$2),$3));}
;

func_api : type ID '(' func_args ')'    {$$=makeSymbol("FUNC_API",concatList(concatList(concatList(concatList($1,$2),$3),$4),$5));} 
;

func_args : func_arglist                {$$=makeSymbol("FUNC_ARGS",$1);}       
            | /* epsilon */             {$$=makeSymbol("FUNC_ARGS", EPSILON);}
;

func_arglist :  func_arglist ',' dcl    {$$=makeSymbol("FUNC_ARGLIST",concatList(concatList($1,$2),$3));} 
                | dcl                   {$$=makeSymbol("FUNC_ARGLIST",$1);}
;

blk : '{' stlist '}'                    {$$=makeSymbol("BLK",concatList(concatList($1,$2),$3));} 
;

dcl :   ID ':' type                     {$$=makeSymbol("DCL",concatList(concatList($1,$2),$3));}
        | ID ',' dcl                    {$$=makeSymbol("DCL",concatList(concatList($1,$2),$3));}
;

type :  INT                             {$$=makeSymbol("TYPE",$1);}
        | FLOAT                         {$$=makeSymbol("TYPE",$1);}
        | VOID                          {$$=makeSymbol("TYPE",$1);} 
;

stlist : stlist stmt                    {$$=makeSymbol("STLIST",concatList($1,$2));}
         |/* epsilon */                 {$$=makeSymbol("STLIST", EPSILON);}
;

stmt :  dcl ';'                         {$$=makeSymbol("STMT",concatList($1,$2));}
        | assn                          {$$=makeSymbol("STMT",$1);}
        | exp ';'                       {$$=makeSymbol("STMT",concatList($1,$2));}
        | cntrl                         {$$=makeSymbol("STMT",$1);}
        | read                          {$$=makeSymbol("STMT",$1);}
        | write                         {$$=makeSymbol("STMT",$1);}
        | return                        {$$=makeSymbol("STMT",$1);}
        | blk                           {$$=makeSymbol("STMT",$1);}
        | BREAK ';'                     {$$=makeSymbol("STMT",concatList($1,$2));}
;

return : RETURN exp ';'                 {$$=makeSymbol("RETURN",concatList(concatList($1,$2),$3));}
        | RETURN ';'                    {$$=makeSymbol("RETURN",concatList($1,$2));}
;

write : WRITE '(' exp ')' ';'           {$$=makeSymbol("WRITE",concatList(concatList(concatList(concatList($1,$2),$3),$4),$5));} 
        | WRITE '(' STR ')' ';'         {$$=makeSymbol("WRITE",concatList(concatList(concatList(concatList($1,$2),$3),$4),$5));} 
;

read : READ '(' lval ')' ';'            {$$=makeSymbol("READ",concatList(concatList(concatList(concatList($1,$2),$3),$4),$5));} 
;

assn : lval ASSIGN exp ';'              {$$=makeSymbol("ASSN",concatList(concatList(concatList($1,$2),$3),$4));} 
;

lval : ID                               {$$=makeSymbol("LVAL",$1);}
;

fullwhile : FULL_WHILE bexp DO stmt     {$$=makeSymbol("FULLWHILE",concatList(concatList(concatList($1,$2),$3),$4));} 
;

cntrl : IF bexp THEN stmt ELSE stmt     {$$=makeSymbol("CNTRL",concatList(concatList(concatList(concatList(concatList($1,$2),$3),$4),$5),$6));} 
        | IF bexp THEN stmt             {$$=makeSymbol("CNTRL",concatList(concatList(concatList($1,$2),$3),$4));} 
        | WHILE bexp DO stmt            {$$=makeSymbol("CNTRL",concatList(concatList(concatList($1,$2),$3),$4));} 
        | fullwhile                     {$$=makeSymbol("CNTRL",$1);}
;

bexp :  bexp OR bexp                    {$$=makeSymbol("BEXP",concatList(concatList($1,$2),$3));}
        | bexp AND bexp                 {$$=makeSymbol("BEXP",concatList(concatList($1,$2),$3));}
        | NOT bexp                      {$$=makeSymbol("BEXP",concatList($1,$2));}
        | exp RELOP exp                 {$$=makeSymbol("BEXP",concatList(concatList($1,$2),$3));}
        | '(' bexp ')'                  {$$=makeSymbol("BEXP",concatList(concatList($1,$2),$3));}
        | fullwhile                     {$$=makeSymbol("BEXP",$1);}
;

exp :   exp ADDOP exp 	                {$$=makeSymbol("EXP",concatList(concatList($1,$2),$3));}
        | exp MULOP exp                 {$$=makeSymbol("EXP",concatList(concatList($1,$2),$3));}
        | '(' exp ')'                   {$$=makeSymbol("EXP",concatList(concatList($1,$2),$3));}
        | '(' type ')' exp              {$$=makeSymbol("EXP",concatList(concatList(concatList($1,$2),$3),$4));}
        | ID                            {$$=makeSymbol("EXP",$1);}
        | num                           {$$=makeSymbol("EXP",$1);}
        | call                          {$$=makeSymbol("EXP",$1);}
;

num :   INTEGERNUM                      {$$=makeSymbol("NUM",$1);}
        | REALNUM                       {$$=makeSymbol("NUM",$1);}
;

call : ID '(' call_args ')'             {$$=makeSymbol("CALL",concatList(concatList(concatList($1,$2),$3),$4));}
;

call_args : call_arglist                {$$=makeSymbol("CALL_ARGS",$1);}
            |/* epsilon */              {$$=makeSymbol("CALL_ARGS", EPSILON);} 
;

call_arglist :  call_arglist ',' exp    {$$=makeSymbol("CALL_ARGLIST",concatList(concatList($1,$2),$3));}
                | exp                   {$$=makeSymbol("CALL_ARGLIST",$1);}
;


%%

void yyerror(const char*) {
    cout << "Syntax error: '" << yytext << "' in line number " << yylineno << endl;
}

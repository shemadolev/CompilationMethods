%{
    #include <iostream>
    #include "part2_helpers.h"
    #include "code_class.hpp"
    using namespace std;

    extern int yylex();
    void yyerror(const char*);

    extern int yylineno;
    extern const char* yytext;

    ParserNode *parseTree; 
    codeClass code;
%}

// Reserved words 
%token  INT
        FLOAT
        VOID
        WRITE
        READ
        WHILE
        DO
        IF
        RETURN
        FULL_WHILE
        BREAK

// Solve dangling else (select shift over reduce)  
%precedence THEN
%precedence ELSE

// Other tokens (no special precedence)
%token ID INTEGERNUM REALNUM STR

//Associativity & precedence matched to C++ syntax
%right ASSIGN
%left OR
%left AND
%left RELOP
%left ADDOP
%left MULOP
%right NOT

// Symbols (Highest priority)
%precedence '(' ')' '{' '}' '.' ';' ',' ':'

%%

program : fdefs                         {parseTree=new NodeSymbol("PROGRAM",$1);}
;

fdefs : /* epsilon */                   {$$=new NodeSymbol("FDEFS", EPSILON);}
	| fdefs func_api blk            {$$=new NodeSymbol("FDEFS",concatList(concatList($1,$2),$3));}
        | fdefs func_api ';'            {$$=new NodeSymbol("FDEFS",concatList(concatList($1,$2),$3));}
;

func_api : type ID '(' func_args ')'    {$$=new NodeSymbol("FUNC_API",concatList(concatList(concatList(concatList($1,$2),$3),$4),$5));} 
;

func_args : func_arglist                {$$=new NodeSymbol("FUNC_ARGS",$1);}       
            | /* epsilon */             {$$=new NodeSymbol("FUNC_ARGS", EPSILON);}
;

func_arglist :  func_arglist ',' dcl    {$$=new NodeSymbol("FUNC_ARGLIST",concatList(concatList($1,$2),$3));} 
                | dcl                   {$$=new NodeSymbol("FUNC_ARGLIST",$1);}
;

blk : '{' stlist '}'                    {$$=new NodeSymbol("BLK",concatList(concatList($1,$2),$3));} 
;

dcl :   ID ':' type                     {$$=new NodeSymbol("DCL",concatList(concatList($1,$2),$3));}
        | ID ',' dcl                    {$$=new NodeSymbol("DCL",concatList(concatList($1,$2),$3));}
;

type :  INT                             {$$=new NodeSymbol("TYPE",$1);}
        | FLOAT                         {$$=new NodeSymbol("TYPE",$1);}
        | VOID                          {$$=new NodeSymbol("TYPE",$1);} 
;

stlist : stlist stmt                    {$$=new NodeSymbol("STLIST",concatList($1,$2));}
         |/* epsilon */                 {$$=new NodeSymbol("STLIST", EPSILON);}
;

stmt :  dcl ';'                         {$$=new NodeSymbol("STMT",concatList($1,$2));}
        | assn                          {$$=new NodeSymbol("STMT",$1);}
        | exp ';'                       {$$=new NodeSymbol("STMT",concatList($1,$2));}
        | cntrl                         {$$=new NodeSymbol("STMT",$1);}
        | read                          {$$=new NodeSymbol("STMT",$1);}
        | write                         {$$=new NodeSymbol("STMT",$1);}
        | return                        {$$=new NodeSymbol("STMT",$1);}
        | blk                           {$$=new NodeSymbol("STMT",$1);}
        | BREAK ';'                     {$$=new NodeSymbol("STMT",concatList($1,$2));}
;

return : RETURN exp ';'                 {$$=new NodeSymbol("RETURN",concatList(concatList($1,$2),$3));}
        | RETURN ';'                    {$$=new NodeSymbol("RETURN",concatList($1,$2));}
;

write : WRITE '(' exp ')' ';'           {$$=new NodeSymbol("WRITE",concatList(concatList(concatList(concatList($1,$2),$3),$4),$5));} 
        | WRITE '(' STR ')' ';'         {$$=new NodeSymbol("WRITE",concatList(concatList(concatList(concatList($1,$2),$3),$4),$5));} 
;

read : READ '(' lval ')' ';'            {$$=new NodeSymbol("READ",concatList(concatList(concatList(concatList($1,$2),$3),$4),$5));} 
;

assn : lval ASSIGN exp ';'              {$$=new NodeSymbol("ASSN",concatList(concatList(concatList($1,$2),$3),$4));} 
;

lval : ID                               {$$=new NodeSymbol("LVAL",$1);}
;

fullwhile : FULL_WHILE bexp DO stmt     {$$=new NodeSymbol("FULLWHILE",concatList(concatList(concatList($1,$2),$3),$4));} 
;

cntrl : IF bexp THEN stmt ELSE stmt     {$$=new NodeSymbol("CNTRL",concatList(concatList(concatList(concatList(concatList($1,$2),$3),$4),$5),$6));} 
        | IF bexp THEN stmt             {$$=new NodeSymbol("CNTRL",concatList(concatList(concatList($1,$2),$3),$4));} 
        | WHILE bexp DO stmt            {$$=new NodeSymbol("CNTRL",concatList(concatList(concatList($1,$2),$3),$4));} 
        | fullwhile                     {$$=new NodeSymbol("CNTRL",$1);}
;

bexp :  bexp OR bexp                    {$$=new NodeSymbol("BEXP",concatList(concatList($1,$2),$3));}
        | bexp AND bexp                 {$$=new NodeSymbol("BEXP",concatList(concatList($1,$2),$3));}
        | NOT bexp                      {$$=new NodeSymbol("BEXP",concatList($1,$2));}
        | exp RELOP exp                 {$$=new NodeSymbol("BEXP",concatList(concatList($1,$2),$3));}
        | '(' bexp ')'                  {$$=new NodeSymbol("BEXP",concatList(concatList($1,$2),$3));}
        | fullwhile                     {$$=new NodeSymbol("BEXP",$1);}
;

exp :   exp ADDOP exp 	                {$$=new NodeSymbol("EXP",concatList(concatList($1,$2),$3));}
        | exp MULOP exp                 {$$=new NodeSymbol("EXP",concatList(concatList($1,$2),$3));}
        | '(' exp ')'                   {$$=new NodeSymbol("EXP",concatList(concatList($1,$2),$3));}
        | '(' type ')' exp              {$$=new NodeSymbol("EXP",concatList(concatList(concatList($1,$2),$3),$4));}
        | ID                            {$$=new NodeSymbol("EXP",$1);}
        | num                           {$$=new NodeSymbol("EXP",$1);}
        | call                          {$$=new NodeSymbol("EXP",$1);}
;

num :   INTEGERNUM                      {$$=new NodeSymbol("NUM",$1);}
        | REALNUM                       {$$=new NodeSymbol("NUM",$1);}
;

call : ID '(' call_args ')'             {$$=new NodeSymbol("CALL",concatList(concatList(concatList($1,$2),$3),$4));}
;

call_args : call_arglist                {$$=new NodeSymbol("CALL_ARGS",$1);}
            |/* epsilon */              {$$=new NodeSymbol("CALL_ARGS", EPSILON);} 
;

call_arglist :  call_arglist ',' exp    {$$=new NodeSymbol("CALL_ARGLIST",concatList(concatList($1,$2),$3));}
                | exp                   {$$=new NodeSymbol("CALL_ARGLIST",$1);}
;


%%

void yyerror(const char*) {
    cout << "Syntax error: '" << yytext << "' in line number " << yylineno << endl;
    exit(2);
}
